apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.github.gmazzo.buildconfig'

// This module is included in two projects:
// - In the root project where it's released as one of our artifacts
// - In build-logic project where we can use it for the samples.
//
// We only want to publish when it's being built in the root project.
if (rootProject.name == 'mosaic') {
	apply plugin: 'com.vanniktech.maven.publish'
} else {
	// Move the build directory when included in build-logic so as to not poison the real build.
	// If we don't there's a chance incorrect build config values (configured below) will be used.
	layout.buildDirectory = new File(rootDir, "build/mosaic-gradle-plugin")
}

dependencies {
	compileOnly gradleApi()
	compileOnly libs.kotlin.gradlePlugin

	testImplementation libs.junit4
	testImplementation libs.truth
	testImplementation gradleTestKit()
}

gradlePlugin {
	plugins {
		mosaic {
			id = "com.jakewharton.mosaic"
			displayName = "Mosaic"
			description = "Jetpack Compose for console UI"
			implementationClass = "com.jakewharton.mosaic.gradle.MosaicPlugin"
		}
	}
}

test {
	dependsOn(':mosaic-gradle-plugin:publishAllPublicationsToTestMavenRepository')
	dependsOn(':mosaic-runtime:publishAllPublicationsToTestMavenRepository')
}

buildConfig {
	useKotlinOutput {
		internalVisibility = true
		topLevelConstants = true
	}

	packageName('com.jakewharton.mosaic.gradle')
	buildConfigField("String", "composeCompilerVersion", "\"${libs.compose.compiler.get().version}\"")
	buildConfigField("String", "mosaicVersion", "\"${project.version}\"")
}
