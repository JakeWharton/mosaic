import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType
import org.jetbrains.kotlin.gradle.plugin.mpp.KotlinMetadataTarget

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply from: "$rootDir/addAllTargets.gradle"
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.jakewharton.mosaic'

kotlin {
	sourceSets {
		commonMain {
			dependencies {
				api libs.jetbrains.compose.runtime
				api libs.kotlinx.coroutines.core
				implementation libs.codepoints
			}
		}
		commonTest {
			dependencies {
				implementation libs.kotlin.test
			}
		}

		concurrentMain {
			dependsOn(commonMain)
		}

		jvmMain {
			dependsOn(concurrentMain)
			dependencies {
				implementation libs.jansi
			}
		}
		nonJvmMain {
			dependsOn(commonMain)
		}
	}

	targets.each { target ->
		if (target instanceof KotlinMetadataTarget) return
		if (target.platformType != KotlinPlatformType.jvm) {
			target.compilations.main.defaultSourceSet.dependsOn(sourceSets.nonJvmMain)
		}
		if (target.platformType == KotlinPlatformType.native) {
			target.compilations.main.defaultSourceSet.dependsOn(sourceSets.concurrentMain)
		}
	}
}
