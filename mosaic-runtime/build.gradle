apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'org.jetbrains.kotlin.plugin.compose'
apply from: "$rootDir/addAllTargets.gradle"
apply from: "$rootDir/publish.gradle"
apply plugin: 'dev.drewhamilton.poko'

kotlin {
	applyDefaultHierarchyTemplate {
		it.common {
			it.group("native") {
				it.group("posix") {
					it.group("linux") {}
					it.group("macos") {}
				}
			}
		}
	}

	explicitApi()

	sourceSets {
		commonMain {
			dependencies {
				api libs.compose.runtime
				api libs.kotlinx.coroutines.core
				implementation libs.compose.collection
				implementation libs.finalizationHook
				implementation libs.mordant.core
				implementation libs.codepoints
			}
		}
		commonTest {
			dependencies {
				implementation libs.kotlin.test
				implementation libs.kotlinx.coroutines.test
				implementation libs.assertk
			}
		}

		jvmMain {
			dependencies {
				implementation libs.jansi
				implementation libs.mordant.jvmJna
			}
		}
	}

	compilerOptions.freeCompilerArgs.add('-Xexpect-actual-classes')
}
