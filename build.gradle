import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

buildscript {
	dependencies {
		classpath libs.kotlin.gradlePlugin
		// We don't use this dependency directly. However, we reference a type from the Kotlin Gradle
		// plugin whose classloading causes com.android.build.gradle.api.BaseVariant to also be loaded.
		classpath libs.android.gradlePlugin
		classpath libs.maven.publish.gradlePlugin
		classpath libs.dokka.gradlePlugin
		classpath libs.buildconfig.gradlePlugin
		classpath 'com.jakewharton.mosaic:mosaic-gradle-plugin'
	}
	repositories {
		mavenCentral()
		google()
		gradlePluginPortal()
	}
}

allprojects {
	group = GROUP
	version = VERSION_NAME

	repositories {
		mavenCentral()
		google()
	}

	tasks.withType(JavaCompile).configureEach {
		sourceCompatibility = JavaVersion.VERSION_1_8
		targetCompatibility = JavaVersion.VERSION_1_8
	}

	tasks.withType(KotlinJvmCompile).configureEach {
		compilerOptions.jvmTarget = JvmTarget.JVM_1_8
	}

	tasks.withType(KotlinCompile).configureEach {
		compilerOptions.freeCompilerArgs.add('-Xjvm-default=all')
	}

	plugins.withId('com.vanniktech.maven.publish') {
		mavenPublish {
			targets {
				installArchives {
					def url = file("${rootProject.buildDir}/localMaven").toURI().toString()
					releaseRepositoryUrl = url
					snapshotRepositoryUrl = url
				}
			}
		}
	}
}
